#include <dsdhacked>
#include <friendly>

strings {
	GOTPLASMA "You got the plasma vulcan!"
}

ammo 2 {
	max 450
	pickup 50
}

#define MTF_BTSX_PLASMAPROJ 600

thing MTF_BTSX_PLASMAPROJ : thing MTF_PLASMA_BALL "fd4rb_BTSXPlasmaProjectile" {
    damage 5
    speed 35

    clear sounds

    seesound ""
    deathsound ""

    states {
        Spawn:
            XPSG C 2 bright
            XPSG D 2 bright
            XPSG E 2 bright
            loop
        Death:
            XPSG AAA 1 bright
            XPSG BBB 1 bright
            stop
    }
}

weapon 5 "fd4rb_PlasmaVulcan" {
    ammotype 2
	ammopershot 1

    states {
        Ready:
            XPLA A 1 A_WeaponReady
            loop
        Deselect:
            XPLA A 1 A_Lower
            loop
        Select:
            XPLA A 1 A_Raise
            loop
        Dryfire:
            XPLA A 6 A_WeaponSound("DRYFR1", 0)
            goto Ready
        Fire:
            TNT1 A 0 {
                A_ClearRefire
                A_CheckAmmo("Dryfire", 1)
            }
        Windup:
            TNT1 A 0 A_WeaponSound("XPLAUP", 0)
        Windup0:
            XPLA BCD 3
            TNT1 A 0 A_CheckAmmo("Winddown0", 1)
        Windup1:
            XPLA ABCD 2
            TNT1 A 0 {
                A_CheckAmmo("Winddown1", 1)
                A_ReFireTo("Windup2")
            }
            goto Winddown1
        Windup2:
            XPLA ABCD 1
            TNT1 A 0 {
                A_CheckAmmo("Winddown2", 1)
                A_ReFireTo("Windup3")
            }
            goto Winddown2
        Windup3:
            XPLA ABCD 1
            TNT1 A 0 {
                A_CheckAmmo("Winddown3", 1)
                A_ReFireTo("Hold")
            }
            goto Winddown3
        Hold:
            XPLA I 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponSoundLoop("XPLAFL", 0, 0x7FFFFFFF)
                A_WeaponProjectileSpread(MTF_BTSX_PLASMAPROJ, 7.2, 4.5)
            }
            TNT1 A 0 {
                A_CheckAmmo("Winddown", 1)
                A_ReFireTo("Hold1")
            }
            goto Winddown
        Hold1:
            XPLA J 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponProjectileSpread(MTF_BTSX_PLASMAPROJ, 7.2, 4.5)
            }
            TNT1 A 0 {
                A_CheckAmmo("Winddown", 1)
                A_ReFireTo("Hold2")
            }
            goto Winddown
        Hold2:
            XPLA K 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponProjectileSpread(MTF_BTSX_PLASMAPROJ, 7.2, 4.5)
            }
            TNT1 A 0 {
                A_CheckAmmo("Winddown", 1)
                A_ReFireTo("Hold3")
            }
            goto Winddown
        Hold3:
            XPLA L 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponProjectileSpread(MTF_BTSX_PLASMAPROJ, 7.2, 4.5)
            }
            TNT1 A 0 {
                A_CheckAmmo("Winddown", 1)
                A_ReFireTo("Hold")
            }
            goto Winddown
        Winddown:
            TNT1 A 0 {
                A_ClearRefire
                A_WeaponSound("XPLAFE", 0)
                A_WeaponSound("XPLADN", 0)
            }
        Winddown3:
            XPLA ABCD 1
            TNT1 A 0 {
                A_CheckAmmo("Winddown2", 1)
                A_ReFireTo("Hold")
            }
        Winddown2:
            XPLA ABCD 1
            TNT1 A 0 {
                A_CheckAmmo("Winddown1", 1)
                A_ReFireTo("Winddown3")
            }
        Winddown1:
            XPLA ABCD 2
            TNT1 A 0 {
                A_CheckAmmo("Winddown0", 1)
                A_ReFireTo("Winddown2")
            }
        Winddown0:
            XPLA ABCD 3
            TNT1 A 0 {
                A_ClearRefire
                A_CheckAmmo("Ready", 1)
                A_ReFireTo("Winddown1")
            }
            goto Ready
        Flash:
            TNT1 A 1 A_LightRandomRange(1, 4)
            TNT1 A 1 A_Light1
            TNT1 A 0 A_Light0
            goto lightDone
    }
}
