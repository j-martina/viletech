#include <dsdhacked>

// Parameters: sound effect ID, whether to play globally, timeout.
custom weapon pointer mbf21 A_WeaponSoundLoop(sound, bool, int)

strings {
    GOTCHAINGUN "You got the tornado battery!"
}

ammo 0 {
	max 350
	pickup 18
}

weapon 3 "fd4rb_TornadoBattery" {
    ammotype 0
    ammopershot 1

    states {
        Ready:
            VCHG A 1 A_WeaponReady
            loop
        Deselect:
            VCHG A 1 A_Lower
            loop
        Select:
            VCHG A 1 A_Raise
            loop
        Dryfire:
            VCHG A 6 A_WeaponSound("DRYFR1", 0)
            goto Ready
        Fire:
            TNT1 A 0 A_CheckAmmo("Dryfire", 1)
            TNT1 A 0 A_CheckAmmo("StartFinalBarrelLeft", 2)
        StartBarrelLeft:
            VCHG B 1 bright {
                A_GunFlash
                A_WeaponSoundLoop("VCHGFL", 0, 0x7FFFFFFF)
                A_ConsumeAmmo(1)
                A_WeaponBulletAttack(2.0, 0.0, 1, 11, 1)
            }
            VCHG C 1
            TNT1 A 0 {
                A_CheckAmmo("FinalBarrelLeftToMiddle", 2)
                A_CheckAmmo("Dryfire", 1)
            }
            VCHG D 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponBulletAttack(0.0, 0.0, 1, 11, 1)
            }
            VCHG E 1
            TNT1 A 0 A_CheckAmmo("FinalBarrelMiddleToRight", 2)
            TNT1 A 0 A_ReFireTo("StartBurstEnd")
            goto FinalBarrelLeftToMiddle
        StartBurstEnd:
            VCHG F 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponBulletAttack(-2.0, 0.0, 1, 11, 1)
            }
            VCHG G 1
            TNT1 A 0 A_CheckAmmo("FinalBarrelRightToMiddle", 2)
            TNT1 A 0 A_ReFireTo("SequenceTwo")
            goto FinalBarrelRightToMiddle
        SequenceTwo:
            VCHG H 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponBulletAttack(2.0, -0.8, 1, 11, 1)
            }
            VCHG E 1
            TNT1 A 0 A_CheckAmmo("FinalBarrelMiddleToLeft", 2)
            TNT1 A 0 A_ReFireTo("SequenceTwoSecond")
            goto FinalBarrelMiddleToLeft
        SequenceTwoSecond:
            VCHG L 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponBulletAttack(4.0, 0.8, 1, 11, 1)
            }
            VCHG C 1
            TNT1 A 0 A_CheckAmmo("FinalBarrelLeftToMiddle", 2)
            TNT1 A 0 A_ReFireTo("SequenceThree")
            goto FinalBarrelLeftToMiddle
        SequenceThree:
            VCHG D 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponBulletAttack(-2.0, 0.8, 1, 11, 1)
            }
            VCHG E 1
            TNT1 A 0 A_CheckAmmo("FinalBarrelMiddleToRight", 2)
            TNT1 A 0 A_ReFireTo("SequenceThreeSecond")
            goto FinalBarrelMiddleToRight
        SequenceThreeSecond:
            VCHG F 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponBulletAttack(-4.0, 0.8, 1, 11, 1)
            }
            VCHG G 1
            TNT1 A 0 A_CheckAmmo("FinalBarrelRightToMiddle", 2)
            TNT1 A 0 A_ReFireTo("SequenceTwo")
            goto FinalBarrelRightToMiddle
        StartFinalBarrelLeft:
            VCHG B 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponBulletAttack(2.0, 0.0, 1, 11, 1)
                A_WeaponSound("VCHGFE", 0)
            }
            VCHG CK 1
            goto Ready
        FinalBarrelMiddleToLeft:
            VCHG L 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponBulletAttack(4.0, -0.8, 1, 11, 1)
                A_WeaponSound("VCHGFE", 0)
            }
            VCHG CK 1
            goto Ready
        FinalBarrelMiddleToRight:
            VCHG F 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponBulletAttack(-4.0, -0.8, 1, 11, 1)
                A_WeaponSound("VCHGFE", 0)
            }
            VCHG GI 1
            goto Ready
        FinalBarrelRightToMiddle:
            VCHG H 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponBulletAttack(-2.0, 0.8, 1, 11, 1)
                A_WeaponSound("VCHGFE", 0)
            }
            VCHG EJ 1
            goto Ready
        FinalBarrelLeftToMiddle:
            VCHG D 1 bright {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponBulletAttack(2.0, -0.8, 1, 11, 1)
                A_WeaponSound("VCHGFE", 0)
            }
            VCHG EJ 1
            goto Ready
        Flash:
            TNT1 A 1 A_Light2
            TNT1 A 1 A_Light1
            TNT1 A 0 A_Light0
            goto LightDone
    }
}
