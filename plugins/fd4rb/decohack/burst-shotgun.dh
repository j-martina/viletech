#include <dsdhacked>

custom weapon pointer mbf21 A_BurstShotgunFire()
custom weapon pointer mbf21 A_BurstShotgunCheckVent(state)
custom weapon pointer mbf21 A_WeaponSoundRandom(sound, sound, sound, sound, bool)

strings {
	GOTSHOTGUN2 "You got the burst shotgun!"
}

ammo 1 {
	max 75
	pickup 6
}

weapon 8 "fd4b_BurstShotgun" {
    ammotype 1
    ammopershot 1

    states {
        Ready:
            TSSG A 1 A_WeaponReady
            loop
        Deselect:
            TSSG A 1 A_Lower
            loop
        Select:
            TSSG A 1 A_Raise
            loop
        Dryfire:
            TSSG A 6 A_WeaponSound("DRYFR1", 0)
            goto Ready
        Fire:
            TNT1 A 0 A_CheckAmmo("Dryfire", 1)
            TSSG B 1 {
                A_ConsumeAmmo(1)
                A_GunFlash
                A_WeaponSoundRandom("TSSGF1", "TSSGF2", "TSSGF3", "TSSGF4", 0)
                A_BurstShotgunFire
            }
            TSSG C 1
            TSSG D 1
            TSSG E 1
            TSSG F 1
            TSSG G 1
            TSSG A 2 A_BurstShotgunCheckVent("GonnaReload")
            goto AfterFiring
        GonnaReload:
            TSSG A 4
            TSSG H 4 A_WeaponSound("PSSGOP", 0)
        ShellsVented:
            TSSG I 1
            TSSG I 1
            TSSG J 1
            TSSG J 1
            TSSG K 1
            TSSG K 1
            TSSG L 1 A_WeaponSound("PSSGCL", 0)
            TSSG L 1
            TSSG A 4
            goto AfterFiring
        AfterFiring:
            TSSG AAAAAAAAAAAAAAAAAA 1 A_WeaponReady
            goto Ready
        Flash:
            TNT1 A 1 A_Light2
            TNT1 A 1 A_Light1
            TNT1 A 0 A_Light0
            goto LightDone
    }
}
